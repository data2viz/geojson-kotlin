
kotlin {
    js {
        moduleName = "geojson-kotlin-$project.name"
        browser {}
    }

    sourceSets {
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
            }
        }
        jsTest {
            dependencies {
                if (project.name != "tests") {
                    implementation kotlin('test-js')
                }
            }
        }
    }
}

def npmDeployDir = file("$buildDir/npm")


task preparePublishNpm(type: Copy, dependsOn: compileKotlinJsLegacy) {
    npmDeployDir.mkdirs()
    from(kotlin.targets.jsLegacy.compilations.main.output.allOutputs)
    from("../build/js/packages/geojson-kotlin-$project.name/package.json")
    into npmDeployDir

    doLast {
        def jsonFile = file("$buildDir/npm/package.json")
        def parsedJson = new groovy.json.JsonSlurper().parseText(jsonFile.text)
        def newFile = new File( npmDeployDir, "package.json")
        newFile.write(generatePackageJson(
                version,
                project.name,
                parsedJson.dependencies))
    }

}

task publishNpm(type: Exec, dependsOn: [preparePublishNpm]) {
    if (!project.hasProperty("npmToken")) {
        logger.warn 'warn: to publish on npm the build needs a npmToken property.'
        return
    }

    workingDir = npmDeployDir
//        commandLine 'npm', 'publish', '--dry-run', "--//registry.npmjs.org/:_authToken=$npmToken"  //test
    commandLine 'npm', 'publish', '--access=public', "--//registry.npmjs.org/:_authToken=$npmToken"
}

static def generatePackageJson(String version, String projectName, Map dependencies) {
    return """
{
    "name": "@data2viz/geojson-kotlin",
    "version" : "$version",
    "description" : "Geojson Kotlin module",
    "author": "Data2viz",
    "main": "geojson-kotlin.js",
    "license": "Apache-2.0",
    "homepage": "https://data2viz.io",
    "bugs": {"url": "https://github.com/data2viz/data2viz/issues"},
    "repository": {
        "type": "git",
        "url": "git+https://github.com/data2viz/data2viz.git"
    },
    "keywords": [
        "Kotlin",
        "JavaScript",
        "data2viz"
    ]
}
""".toString()
}
